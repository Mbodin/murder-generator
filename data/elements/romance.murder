
category Romance
begin
  translation fr "Romance"
  translation en "Romance"
  translation eo "Amrakonto"

  description fr "Histoires d’amour."
  description en "Love stories."
  description eo "Rakontoj pri amo."
end

contact Relation Love
begin
  category Romance

  translation fr "amoureuse"
  translation en "Love"
  translation eo "Amrilato"
end

element TwoPeopleFallingInLove
begin
  let A be player with contact FamilyRelation to B as None
                  with contact Relation to any other player not as Love
  let B be player with contact FamilyRelation to A as None
                  with contact Relation to any other player not as Love

  provide event lasting seconds to A and B
  begin
    event Personal
    assume no event providing contact Know from A to B before
    assume no event providing contact Know from B to A before

    sentence
    begin
      translation eo "Dum trajnvojaĝo, " A " kaj " B " vidis unu la alian per fenestro."
      translation fr "Lors d’un voyage en train, " A " et " B " se sont aperçus par la fenêtre."
      translation en "During a train trip, " A " and " B " saw each other through a window."
    end

    sentence
    begin
      translation eo "En la unua fulmrigardo, ili tuj enamiĝis unu la alian."
      translation fr "Coup de foudre immédiat !"
      translation en "They immediately fell in love."
    end

    sentence
    begin
      translation eo "Bedaŭrinde, ili estis en malsamaj trajnoj kaj ne povis kontaktiĝi unu kun la alia."
      translation fr "Malheureusement, ils était dans des trains différents et n’ont pas pu échanger."
      translation en "Unfortunately, they were on different trains and couldn’t make contact."
    end

    sentence
    begin
      translation eo "Ili havos la vizaĝon de unu la alia en la kapo kun espero iam rerenkontiĝi."
      translation fr "Ils garderons le visage de l’autre dans leurs mémoires dans l’espoir de se rencontrer à nouveau."
      translation en "They shall keep each other’s face into their memory, hoping to meet again."
    end

    provide contact Relation between A and B as Love
    provide compatible contact Know between A and B as True
  end

  provide relation between A and B as trust
end

element FallInLoveAfterAParty
begin
  let A be player with contact Know to C as True
                  with contact FamilyRelation to B as None
  let B be player with contact Know to C as True
                  with contact FamilyRelation to A as None
  let C be player with contact Know to A as True
                  with contact Know to B as True
                  with contact Know to I1 as True
                  with contact Know to I2 as True
  let I1 be player with contact Know to C as True
  let I2 be player with contact Know to C as True

  provide event lasting minutes to C
  begin
    event Personal

    sentence
    begin
      translation eo C:+sbeg " decidis fari feston, kaj invitis multe da konatuloj."
      translation fr C:+sbeg " a organisé une grande fête où " C:+pro " invitait de nombreuses connaissances."
      translation en C:+sbeg " organised a large party, inviting a large number of acquaintances."
    end

    sentence
    begin
      translation eo "Estis multe da invitantoj: " C:+pro " ne memoras ĉiujn el ili."
      translation fr "Il y avait beaucoup d’invités : " C:+pro " ne se souvient plus de tout le monde."
      translation en "There were quite a lot of people coming: " C:+pro " no longer remembers who came."
    end

    sentence
    begin
      translation eo "Sed " C:+pro " ja memoras pri inviti " A:+acc ", " B:+acc ", kaj " I1:+acc "."
      translation eo "Sed " C:+pro " ja memoras pri inviti " I1:+acc ", " I2:+acc ", kaj " B:+acc "."
      translation eo "Sed " C:+pro " ja memoras pri inviti " A:+acc " kaj " I1:+acc "."
      translation eo "Sed " C:+pro " ja memoras pri inviti " I2:+acc " kaj " B:+acc "."
      translation fr "Mais " C:+pro " se souvient par exemple avoir invité " A ", " B ", et " I1 "."
      translation fr "Mais " C:+pro " se souvient par exemple avoir invité " I1 ", " I2 ", et " B "."
      translation fr "Mais " C:+pro " se souvient par exemple avoir invité " A " et " I1 "."
      translation fr "Mais " C:+pro " se souvient par exemple avoir invité " I2 " et " B "."
      translation en C:+sbeg:+pro " remembers quite well to have invited " A ", " B ", and " I1 "."
      translation en C:+sbeg:+pro " remembers quite well to have invited " I1 ", " I2 ", and " B "."
      translation en C:+sbeg:+pro " remembers quite well to have invited " A " and " I1 "."
      translation en C:+sbeg:+pro " remembers quite well to have invited " I2 " and " B "."
    end
  end

  provide event lasting minutes to I1
  begin
    event Personal

    sentence
    begin
      translation eo I1:+sbeg " estis invitita de " C " por iu festo."
      translation fr I1:+sbeg " a été invité" I1:+agr " par " C " à une fête."
      translation en I1:+sbeg " was invited by " C " to a party."
    end

    sentence
    begin
      translation eo "Estis agrabla festo."
      translation fr "C’était sympa."
      translation en "It was a nice party."
    end
  end

  provide event lasting minutes to I2
  begin
    event Personal

    sentence
    begin
      translation eo C:+sbeg " invitis " I2:+acc " por festi ion."
      translation fr C:+sbeg " a invité " I2 " à fêter quelque chose."
      translation en C:+sbeg " invited " I2 " to party."
    end

    sentence
    begin
      translation eo "Estis simpatia de " C:+pro "."
      translation fr "C’était sympa d’avoir pensé à " I2:+pro "."
      translation en "It was nice of " C:+cli "."
    end
  end

  provide event lasting minutes to A and B
  begin
    event Personal
    assume no event providing contact Know from A to B before
    assume no event providing contact Know from B to A before

    sentence
    begin
      translation eo "Dum festo organizita de " C ", " A " kaj " B " renkontiĝis."
      translation fr A:+sbeg " et " B " se sont rencontrés lors d’une fête organisée par " C "."
      translation fr A:fem:+sbeg " et " B:fem " se sont rencontrées lors d’une fête organisée par " C "."
      translation en A:+sbeg " and " B " met during a party organised by " C "."
    end

    sentence
    begin
      translation eo "Ili ŝategis babili unu kun la alia kaj interŝanĝis siajn kontaktojn."
      translation fr "Ils se sont vite rendus compte qu’ils partagent beaucoup de choses et se sont échangés leurs contacts."
      translation en "They greatly appreciated chatting one with the other and exchanged their contacts."
    end

    provide contact Know between A and B as True
  end

  provide phantom event lasting days to A and B

  provide event lasting days to A and B
  begin
    event Personal

    sentence
    begin
      translation eo "Rerenkontiĝante unu la alian, " A " kaj " B " entute enamiĝis."
      translation fr "Lors d’un nouveau rendez-vous, " A " et " B " sont tombés amoureux."
      translation fr "Lors d’un nouveau rendez-vous, " A:fem " et " B:fem " sont tombées amoureuses."
      translation en "Meeting once again each other, " A " and " B " fell in love."
    end

    provide contact Relation between A and B as Love
  end

  provide phantom event lasting days to A and B

  provide relation between A and B as strong trust
end

element LoveAtFirstSight
begin
  let A be player with contact FamilyRelation to B as None
                  with contact Relation to any other player not as Love
  let B be player with contact FamilyRelation to A as None

  provide event lasting seconds to A
  begin
    event Personal

    provide compatible contact Goal from A to B as Know
    provide contact Relation from A to B as Love

    sentence
    begin
      translation eo A:+sbeg " bone memoras kiam " A:+pro " vidis " B:+acc " por la unua fojo."
      translation fr A:+sbeg " se souviens très bien du moment où " A:+pro " a vu " B " pour la première fois."
      translation en A:+sbeg " remembers well how " A:+pro " saw " B " for the first time."
    end

    sentence
    begin
      translation eo "Nu, " A:+pro " ĉefe memoras pri " B:+pro ", fakte."
      translation fr "Enfin, " A:+pro " se souviens surtout de " B:mas:+cli ", pour être honnête."
      translation fr "Enfin, " A:+pro " se souviens surtout d’" B:+cli ", pour être honnête."
      translation en "To be honest " A:+pro " mostly remembers about " B:+cli "."
    end

    sentence
    begin
      translation eo "Tiam la cetero de la mondo kvazaŭ malaperis."
      translation fr "À ce moment, c’était comme si le reste du monde s’était effacé."
      translation en "At this time, it was as if the rest of the world was fading away."
    end

    sentence
    begin
      translation eo "Nur gravis unu afero: " B:+pro "."
      translation fr "Seul importait une seule personne : " B:+cli "."
      translation en "Only one person then mattered: " B:+cli "."
    end

    sentence
    begin
      translation eo "Iu prononcis " B:+pos:+acc " nomon: " B "."
      translation fr "Quelqu’un prononçait son nom : " B "."
      translation en "Someone was calling " B:+pos " name: " B "."
    end

    sentence
    begin
      translation eo "Kia bela nomo."
      translation fr "Quel beau nom."
      translation en "What a beautiful name."
    end

    sentence
    begin
      translation eo A:+sbeg:+pro " tuj enamiĝis."
      translation fr A:+sbeg:+pro " est tout de suite tombé" A:fem:+agr " amoureuse."
      translation fr A:+sbeg:+pro " est tout de suite tombé" A:+agr " amoureux."
      translation en A:+sbeg:+pro " immediately fell in love."
    end

    sentence
    begin
      translation eo "Bedaŭrinde " A:+pro " ne povis babili kun " B:+pro ": oni estis alvokanta " B:+pro:+acc " kaj " B:+pro " eliris tiudirekten."
      translation fr "Malheureusement " A:+pro " n’a pas pu discuter avec " B:+cli " : quelqu’un l’appelait et " B:+pro " s’en est allé" B:+agr "."
      translation en "Unfortunately " A:+pro " couldn’t discuss with " B:+cli ": someone was calling " B:+cli " and " B:+pro " went away."
    end
  end

  provide phantom event lasting days to A

  provide event lasting days to A
  begin
    event Personal
    assume no event providing contact Know from B to A before

    sentence
    begin
      translation eo "Ekde kiam " A " vidis " B:+acc " por la unua fojo, " A:+pro " ne ĉesis pensi pri " B:+pro "."
      translation fr "Depuis que " A " a vu " B " pour la première fois, " A:+pro " n’a pas arrêté de penser à " B:+cli "."
      translation en "Since " A " saw " B " for the first time, " A:+pro " never stopped thinking about " B:+cli "."
    end

    sentence
    begin
      translation eo "Kiu " B:+pro " estas?"
      translation fr "Qui est " B:+pro " ?"
      translation en "Who is " B:+pro "?"
    end

    sentence
    begin
      translation eo "Kie " B:+pro " loĝas?"
      translation fr "Où loge-t-" B:+pro " ?"
      translation en "Where does " B:+pro " live?"
    end

    sentence
    begin
      translation eo "Kion " B:+pro " faras?"
      translation fr "Que fait-" B:+pro " ?"
      translation en "What does " B:+pro " do?"
    end

    sentence
    begin
      translation eo A:+sbeg:+pro " volas revidi " B:+pro:+acc "."
      translation fr B:fem:+empty A:+sbeg:+pro " veut la revoir."
      translation fr B:mas:+empty A:+sbeg:+pro " veut le revoir."
      translation fr A:+sbeg:+pro " veut le·a revoir."
      translation en A:+sbeg:+pro " wants to see " B:+cli " again."
    end

    sentence
    begin
      translation eo A:+sbeg:+pro " volas koni " B:+pro:+acc "."
      translation fr B:fem:+empty A:+sbeg:+pro " veut la connaître."
      translation fr B:mas:+empty A:+sbeg:+pro " veut le connaître."
      translation fr A:+sbeg:+pro " veut le·a connaître."
      translation en A:+sbeg:+pro " wants to know " B:+cli "."
    end
  end

  provide relation from A to B as strong trust
  provide relation from B to A as strong neutral
end

